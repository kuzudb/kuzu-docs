---
title: PageRank
---

The PageRank algorithm ranks entities in a network based on their importance by analyzing connections
between them. Each entity (e.g., nodes in a social network, documents, or proteins) gets a score, with
higher scores for those linked to by other high-scoring entities. Scores are iteratively calculated,
distributing an entity’s rank across its outgoing connections, until stable. In general domains, like
citation networks or recommendation systems, it identifies influential nodes, such as highly cited
papers or key influencers.

Kuzu implements a parallelized version of PageRank based on [Ligra](https://jshun.csail.mit.edu/ligra.shtml).

## Syntax

```
CALL page_rank(
    graph_name,
    dampingFactor := 0.85, 
    maxIterations := 20, 
    tolerance := 0.0000001
    normalizeInitial := true
)
RETURN node, rank
```

**Alias**: `pr`

#### Parameters

| Parameter | Type | Optional | Default | Description |
|-----------|------|----------|---------|-------------|
| graph_name | STRING | No | - | Name of the projected graph to run the algorithm on |
| dampingFactor | FLOAT64 | Yes | 0.85 | Probability of following an outgoing edge, must between 0 and 1 |
| maxIterations | INT64 | Yes | 20 | Maximum number of iterations to run |
| tolerance | FLOAT64 | Yes | 0.00001 | Minimum change in scores between iterations to continue |
| normalizeInitial | BOOLEAN | Yes | true | Whether to normalize initial scores to sum to 1 |


#### Return

| Column | Type | Description |
|--------|------|-------------|
| node | NODE | Node object |
| rank | FLOAT64 | PageRank score of the node |

## Example

Define schema
```cypher
CREATE NODE TABLE Person(name STRING PRIMARY KEY);
CREATE REL TABLE KNOWS(FROM Person to Person);
```
Insert nodes and edges
```cypher
CREATE (u0:Person {name: 'Alice'}),
        (u1:Person {name: 'Bob'}),
        (u2:Person {name: 'Charlie'}),
        (u3:Person {name: 'Derek'}),
        (u4:Person {name: 'Eve'}),
        (u0)-[:KNOWS]->(u1),
        (u0)-[:KNOWS]->(u4),
        (u1)-[:KNOWS]->(u4),
        (u2)-[:KNOWS]->(u4),
        (u3)-[:KNOWS]->(u4);
```
We'll first create a projected graph from the node and edge tables.
```cypher
CALL project_graph('Graph', ['Person'], ['KNOWS']);
```
Run PageRank on the projected graph as follows:
```cypher
CALL page_rank('Graph')
RETURN node.name, rank
ORDER BY rank DESC;
```
```
┌───────────┬──────────┐
│ node.name │ rank     │
│ STRING    │ DOUBLE   │
├───────────┼──────────┤
│ Eve       │ 0.130088 │
│ Bob       │ 0.042750 │
│ Derek     │ 0.030000 │
│ Charlie   │ 0.030000 │
│ Alice     │ 0.030000 │
└───────────┴──────────┘
```
